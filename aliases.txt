@rem !!! Do not change this file in-place, change its copy instead !!!
@rem !!!  Otherwise you will lose your settings after next update  !!!

@echo off

rem Simple "ver" prints empty line before Windows version
rem Use this construction to print just a version info
REM cmd /d /c ver | "%windir%\system32\find.exe" "Windows"

rem Now we form the command prompt

rem This will start prompt with `User@PC `
REM if /I "%ConEmuPromptNames%" == "NO" (
  REM set ConEmuPrompt0=
REM ) else (
  REM set ConEmuPrompt0=$E[m$E[32m$E]9;8;"USERNAME"$E\@$E]9;8;"COMPUTERNAME"$E\$S
REM )

REM Remove the user name before path
set ConEmuPrompt0=

rem Followed by colored `Path`
set ConEmuPrompt1=%ConEmuPrompt0%$E[92m$P$E[90m
if NOT "%PROCESSOR_ARCHITECTURE%" == "AMD64" (
  if "%PROCESSOR_ARCHITEW6432%" == "AMD64" if "%PROCESSOR_ARCHITECTURE%" == "x86" (
    rem Use another text color if cmd was run from SysWow64
    set ConEmuPrompt1=%ConEmuPrompt0%$E[93m$P$E[90m
  )
)

rem Carriage return and `$` or `#`
if /I "%ConEmuPromptNL%" == "NO" (set ConEmuPrompt2=) else (set ConEmuPrompt2=$_)
rem Spare `$E[90m` was specially added because of GitShowBranch.cmd
if "%ConEmuIsAdmin%" == "ADMIN" (
  set ConEmuPrompt2=%ConEmuPrompt2%$E[90m#
) else (
  set ConEmuPrompt2=%ConEmuPrompt2%$E[90m$$
)

rem Finally reset color and add notify ConEmu about prompt input start coords
set ConEmuPrompt3=$E[m$S$E]9;12$E\

rem Show 'git status' extract in prompt?
if /I "%~1" == "/git" goto git
if /I "%~1" == "-git" goto git
goto no_git
:git
shift /1
call "%~dp0GitShowBranch.cmd" /i
goto :end_git
:no_git
rem Set new prompt
PROMPT %ConEmuPrompt1%%ConEmuPrompt2%%ConEmuPrompt3%

REM Custom aliases

doskey c = cls

doskey art = php artisan $*
doskey mfs = php artisan migrate:fresh --seed $*

doskey nr = npm run $*
doskey nis = npm install --save $*
doskey nus = npm uninstall --save $*
doskey nid = npm install --save-dev $*
doskey nud = npm uninstall --save-dev $*

doskey gs = git status $*
doskey gb = git branch $*
doskey gco = git checkout $*
doskey gcm = git commit -m $*
doskey gcam = git commit -am $*

doskey t = .\vendor\bin\phpunit $*

doskey .. = cd ..
doskey ... = cd ..\..\
doskey .... = cd ..\..\..\
doskey ..... = cd ..\..\..\..\
